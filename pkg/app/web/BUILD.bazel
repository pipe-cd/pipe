load("@npm//webpack-cli:index.bzl", webpack = "webpack_cli")
load("@npm//@bazel/typescript:index.bzl", "ts_project")
load("@npm//@bazel/labs:index.bzl", "ts_proto_library")
load(":jest.bzl", "jest_test")

ts_proto_library(
    name = "api_proto",
    proto = "//pkg/app/api/service/webservice:webservice_proto",
)

ts_proto_library(
    name = "model_proto_ts",
    proto = "//pkg/model:model_proto",
)

genrule(
    name = "build_api",
    srcs = [":api_proto"],
    outs = ["api_client"],
    cmd = """
    mkdir $(OUTS)
    for f in $(SRCS)
    do
        sed -ie "s|.*validate_pb.*||g" $$f
        sed -ie "s|'.*/model/|'pipe/pkg/app/web/model/|g" $$f
        sed -ie "s|.*github_com_gogo_protobuf_gogoproto_gogo_pb.*||g" $$f
        cp $$f $(OUTS)/
    done
    """,
    output_to_bindir = 1,
    visibility = ["//visibility:public"],
)

genrule(
    name = "build_model",
    srcs = [":model_proto_ts"],
    outs = ["model"],
    cmd = """
    mkdir $(OUTS)
    for f in $(SRCS)
    do
        sed -ie "s|.*validate_pb.*||g" $$f
        sed -ie "s|'.*pkg|'pipe/pkg|g" $$f
        sed -ie "s|.*github_com_gogo_protobuf_gogoproto_gogo_pb.*||g" $$f
        sed -ie "s|pipe/pkg/model|pipe/pkg/app/web/model|g" $$f
        cp $$f $(OUTS)/
    done
    """,
    output_to_bindir = 1,
    visibility = ["//visibility:public"],
)

ts_project(
    name = "compile",
    srcs = glob(
        [
            "src/**/*",
        ],
        exclude = [
            "**/__mocks__/**",
            "**/__fixtures__/**",
            "src/**/*.test.*",
            "src/mocks/**",
            "src/**/*.stories.*",
        ],
    ) + [
        ":build_api",
        ":build_model",
    ],
    allow_js = True,
    tsconfig = ":tsconfig.json",
    deps = [
        "@npm//@material-ui",
        "@npm//@primer/octicons-react",
        "@npm//@reduxjs/toolkit",
        "@npm//@types/dagre",
        "@npm//@types/google-protobuf",
        "@npm//@types/history",
        "@npm//@types/jest",
        "@npm//@types/node",
        "@npm//@types/react",
        "@npm//@types/react-dom",
        "@npm//@types/react-redux",
        "@npm//@types/react-router",
        "@npm//@types/react-router-dom",
        "@npm//@types/yup",
        "@npm//clsx",
        "@npm//copy-to-clipboard",
        "@npm//dayjs",
        "@npm//formik",
        "@npm//grpc-web",
        "@npm//react-cookie",
        "@npm//react-draggable",
        "@npm//react-intersection-observer",
        "@npm//redux",
        "@npm//redux-thunk",
    ],
)

genrule(
    name = "copy_assets",
    srcs = glob(["assets/**/*"]),
    outs = ["assets"],
    cmd = """
    mkdir $(OUTS)
    for f in $(SRCS)
    do
        cp $$f $(OUTS)/
    done
    """,
    output_to_bindir = 1,
    visibility = ["//visibility:public"],
)

webpack(
    name = "public_files",
    args = [
        "--config",
        "$(execpath webpack.config.js)",
        "--html-template",
        "$(execpath base.html)",
        "--bazel-bin-path",
        "$(BINDIR)",
        "--output-path",
        "$(@D)",
        "$(execpath src/index.js)",
    ],
    data = [
        "api_client",
        "src/index.js",
        "compile",
        "base.html",
        "copy_assets",
        "model",
        "webpack.common.js",
        "webpack.config.js",
        "@npm//:node_modules",
    ] + glob(["assets/**/*"]),
    output_dir = True,
    visibility = ["//visibility:public"],
)

jest_test(
    name = "test",
    size = "small",
    srcs = glob([
        "src/**/*.ts",
        "src/**/*.snap",
    ]),
    jest_config = ":jest.config.js",
    deps = [
        ":build_api",
        ":build_model",
        ":file-transformer.js",
        ":jest.after-env.ts",
        ":jest.setup.js",
        ":test-utils.tsx",
        ":tsconfig.json",
        "@npm//:node_modules",
    ],
)
