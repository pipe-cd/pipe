load("@npm//webpack-cli:index.bzl", webpack = "webpack_cli")
load("@npm//typescript:index.bzl", "tsc")
load("@npm_bazel_labs//:index.bzl", "ts_proto_library")
load(":jest.bzl", "jest_test")

ts_proto_library(
    name = "api_proto",
    proto = "//pkg/app/api/service/webservice:webservice_proto",
)

ts_proto_library(
    name = "model_proto_ts",
    proto = "//pkg/model:model_proto",
)

genrule(
    name = "build_api",
    srcs = [":api_proto"],
    outs = ["api_client"],
    cmd = """
    mkdir $(OUTS)
    for f in $(SRCS)
    do
        sed -ie "s|.*validate_pb.*||g" $$f
        sed -ie "s|'.*pkg|'pipe/pkg|g" $$f
        sed -ie "s|.*github_com_gogo_protobuf_gogoproto_gogo_pb.*||g" $$f
        sed -ie "s|pipe/pkg/model|pipe/pkg/app/web/model|g" $$f
        cp $$f $(OUTS)/
    done
    """,
    output_to_bindir = 1,
    visibility = ["//visibility:public"],
)

genrule(
    name = "build_model",
    srcs = [":model_proto_ts"],
    outs = ["model"],
    cmd = """
    mkdir $(OUTS)
    for f in $(SRCS)
    do
        sed -ie "s|.*validate_pb.*||g" $$f
        sed -ie "s|'.*pkg|'pipe/pkg|g" $$f
        sed -ie "s|.*github_com_gogo_protobuf_gogoproto_gogo_pb.*||g" $$f
        sed -ie "s|pipe/pkg/model|pipe/pkg/app/web/model|g" $$f
        cp $$f $(OUTS)/
    done
    """,
    output_to_bindir = 1,
    visibility = ["//visibility:public"],
)

tsc(
    name = "compile",
    args = [
        "--project",
        "$(location :tsconfig.json)",
        "--outDir",
        "$(@D)",
    ],
    data = glob(
        [
            "src/**/*",
        ],
        exclude = [
            "**/__mocks__/**",
            "**/__fixtures__/**",
            "src/**/*.test.*",
            "src/**/*.stories.*",
        ],
    ) + [
        ":build_api",
        ":build_model",
        ":tsconfig.json",
        "@npm//:node_modules",
    ],
    output_dir = True,
)

genrule(
    name = "copy_assets",
    srcs = glob(["assets/**/*"]),
    outs = ["assets"],
    cmd = """
    mkdir $(OUTS)
    for f in $(SRCS)
    do
        cp $$f $(OUTS)/
    done
    """,
    output_to_bindir = 1,
    visibility = ["//visibility:public"],
)

webpack(
    name = "public_files",
    args = [
        "--config",
        "$(execpath webpack.config.js)",
        "--html-template",
        "$(execpath base.html)",
        "$(location compile)/index.js",
        "--bazel-bin-path",
        "$(BINDIR)",
        "--output-path",
        "$(@D)",
    ],
    data = [
        "api_client",
        "base.html",
        "compile",
        "copy_assets",
        "model",
        "webpack.common.js",
        "webpack.config.js",
        "@npm//:node_modules",
    ] + glob(["assets/**/*"]),
    output_dir = True,
    visibility = ["//visibility:public"],
)

jest_test(
    name = "test",
    size = "small",
    srcs = glob([
        "src/**/*.ts",
        "src/**/*.snap",
    ]),
    jest_config = ":jest.config.js",
    deps = [
        ":build_api",
        ":build_model",
        ":file-transformer.js",
        ":jest.after-env.ts",
        ":jest.setup.js",
        ":tsconfig.json",
        "@npm//:node_modules",
    ],
)
